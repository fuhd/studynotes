使用task
========================
### 声明task动作
动作（action）就是在task中合适的地方文墨构建逻辑。Task接口（task由Task接口定义）提供了两个相关
的方法来声明task动作：`doFirst(Closure)`和`doLast(Closure)`。当task被执行的时候，动作逻辑
被定义为闭包参数，被依次执行。示例：
```gradle
version = '0.1-SNAPSHOT'
task printVersion {
  doLast{
    println "Version: $version"
  }
}
```
这个task的作用就是打印出当前的项目版本，这段逻辑放在doLast方法里面的。左移操作符（`<<`）就是`doLast`
方法的快捷版本。上面的示例与下面示例相同：
```gradle
version = '0.1-SNAPSHOT'
task printVersion << {
  println "Version: $version"
}
```
当然你把这段逻辑放到`doFirst`方法里面，也会得到同样的结果，示例如下：
```gradle
version = '0.1-SNAPSHOT'
task printVersion {
  doFirst{
    println "Version: $version"
  }
}
```
目前， 上面的示例我们只为task添加了一个动作doLast或者doFirst，但并不限于为每个task只添加一个动作。
事实上，在task创建后，你可以根据需要添加很多动作，doFirst先执行，doLast后执行。。示例：
```gradle
version = '0.1-SNAPSHOT'
task printVersion{
  doFirst{
    println "Before reading the project version"
  }
  doLast{
    println "Version: $version"
  }
}
printVersion.doFirst{println "First action"}
printVersion << {println "Last action"}
```
**最后加的doFirst最先执行，最后加的doLast最后执行！！！**

### 访问DefaultTask属性
有两个属性：`group`和`description`。它们都是task属性，`Task`接口默认由`DefaultTask`类实现。
这两个属性就是来自于`DefaultTask`类。`description`属性用于描述任务的作用，`group`属性则用于
定义task的逻辑分组。在创建task的时候，可以为这两个属性设置值作为参数，示例：
```gradle
task printVersion(group:'versioning',description:'prints project version.') << {
  //Gradle提供了一个基于SLF4J日志库的logger实现。通过Task的方法可以直接访问logger实例。
  logger.guiet "Version: $version"
}
```
或者:
```gradle
task printVersion{
  group = 'versioning'
  description = 'prints project version.'
  doLast{
    logger.quiet "Version: $version"
  }
}
```
当运行`gradle tasks`命令时，我们可以看到task正确的分组和描述信息。

### 定义task依赖
`dependsOn`方法允许声明依赖一个或多个task。示例：
```gradle
task first << {println "first"}
task second << {println "second"}
task printVersion(dependsOn: [second,first]) << {   //指定多个task依赖
  logger.quiet "Version: $version"
}
task third << {println "third"}
third.dependsOn('printVersion')   //声明依赖时按名称引用task
```
**注意：Gradle并不能保证task依赖的执行顺序**

上面示例，first与second哪个先执行，并不确定。`dependsOn`方法只是定义了所依赖的task需要先执行
，并没有定义它该如何执行。

### 终结器task
在实践中，你会发现所依赖的task执行后需要做一些其他处理。针对这种情况，Gradle提供了终结器（finalizer task）
，即使终结器失败了，Gradle的task也会按照预期运行（就好像开了一个子线程处理一些其他任务）。我们通过
Task方法`finalizedBy`来使用一个特定的终结器，示例：
```gradle
task first << {println "first"}
task second << {println "second"}
first.finalizedBy second    //就是执行完first就会触发second
```

### 添加任意代码
在Gradle脚本中定义通用的`Groovy`代码，示例：
```gradle
version = new ProjectVersion(0,1)
class ProjectVersion{
  Integer major
  Integer minor
  Boolean release
  ProjectVersion(Integer major,Integer minor){
    this.major = major
    this.minor = minor
    this.release = Boolean.FALSE
  }
  ProjectVersion(Integer major,Integer minor,Boolean release){
    this(major,minor)
    this.release = release
  }
  @Override
  String toString(){
    "$major.$minor${release?'':'-SNAPSHOT'}"
  }
}
```

### 理解task配置
#### 添加task配置块
示例：
```gradle
//version.properties是当前目录下的一个版本配置文件
//Project接口提供了file方法，它会创建一个相对于项目目录的java.io.File实例
ext.versionFile = file('version.properties')
task loadVersion{
  //定义task配置(没有使用左移操作符)
  project.version = readVersion()
}
ProjectVersion readVersion(){
  logger.guiet 'Reading the version file.'
  if(!versionFile.exists()){
    throw new GradleException("Required version file does not exist: $versionFile.canonicalPath")
  }
  Propertis versionProps = new Propertis()
  versionFile.withInputStream{ stream ->
    versionProps.load(stream)
  }
  //在Groovy中，如果return是方法中最后一条语句的话，则可以将return省略
  new ProjectVersion(versionProps.major.toInteger(),versionProps.minor.toInteger(),
    versionProps.release.toBoolean())
}
```
乍一看，loadVersion这个task很像之前定义的其他task，但是如果仔细观察就会发现没有定义动作或者使用
左移操作符（`<<`），Gradle称之为 **task配置** 。**task配置块永远在task动作执行之前被执行**。
#### Gradle构建生命周期阶段
无论什么时候执行Gradle构建，都会运行三个不同的生命周期阶段：**初始化**,**配置** 和 **执行**。
在初始化阶段，Gradle为项目创建了一个`Project`实例。在给定的构建脚本中只定义了一个project。在多项目
构建中，这个构建阶段变得更加重要。初始化阶段后面紧接着的是配置阶段。

> 注意：项目每一次构建的任何配置代码都可以被执行————即使你只是执行`gradle tasks`

在执行阶段，所有的task都应该以正确的顺序被执行。执行顺序是由它们的依赖决定的。如果任务被认为没有修改
过，将被跳过。
#### 声明task的inputs和outputs
Gradle通过比较两个构建task的`inputs`和`outputs`来决定task是否是最新的。自从最后一个task执行以来，
如果`inputs`和`outputs`没有发生变化，则认为task是最新的。因此，只有当`inputs`和`outputs`不同时，
task才运行；否则将跳过。注：**`inputs`和`outputs`在`DefaultTask`类中被定义为属性。**
> inputs可以是一个目录，一个或多个文件，或者是一个任意属性。inputs是TaskInputs接口的实现，作为
> task也就是DefaultTask类的一个属性。TaskInputs的方法有：
> + dir(dirPath: Object)
> + file(path: Object)
> + files(paths: Object...)
> + property(name: String,value: Object)

> outputs可以是通过一个目录或1～n个文件来定义的。outputs是TaskOutputs接口的实现，作为task也就是
> DefaultTask类的一个属性。TaskOutputs的方法有：
> + dir(dirPath: Object)
> + file(path: Object)
> + files(paths: Object...)

假设你想创建一个task，为产品发布准备项目的可交付版本。为此，你想将项目版本从SNAPSHOT改变为release。示例：
```gradle
task makeReleaseVersion(group: 'versioning',description: 'Makes project a release version.') << {
  version.release = true
  //Ant Task的propertyfile提供了一种便利的方式来修改属性文件
  ant.propertyfile(file: versionFile){
    entry(key:'release',type: 'String', operation: '=',value: 'true')
  }
}
```
注意，第一次运行makeReleaseVersion task后，虽然我们将项目版本标记为产品发布版本，但是Gradle并不知道。也就
是说，Gradle并不知道makeReleaseVersion task是最新的，并不需要再执行，可以直接跳过。为了解决这个问题，需要
声明它的inputs和outputs。示例如下：
```gradle
//在配置阶段声明inputs/outputs，这里没有使用`<<`左移操作符的定义
task makeReleaseVersion(group: 'versioning',description: 'Makes project a release version.'){
  //声明版本的release属性作为输入
  inputs.property('release',version.release)
  //由于版本文件被修改了，所以它被声明作为输出文件属性
  outputs.file versionFile
  doLast{
    version.release = true
    ant.propertyfile(file: versionFile){
      entry(key: 'release',type: 'String',operation: '=',value: 'true')
    }
  }
}
```
你把想执行的代码放到doLast动作闭包中，并从task声明中移除左移操作符。这样，你就清晰地分离了它的配置和动作代码。
现在，如果你执行task两次，就会发现Gradle已经知道项目版本被设置为发布版本，而且自动跳过task的第二次执行。

### 编写和使用自定义任务
自定义task包含两个组件：自定义的task类，封装了逻辑行为，也被称作任务类型；以及真实的task，提供了用于
配置行为的task类所暴露的属性值。Gradle把这些task称为 **增强的task**。
可维护性是编写自定义task类的优势之一。与简单的task相比，增强的task的另一个优势是可重用性。
#### 编写自定义的task类
当创建一个自定义task时，你需要做的是，创建一个继承`DefaultTask`的类。示例：
```gradle
class ReleaseVersionTask extends DefaultTask{
  //通过注解声明task的输入
  @Input Boolean release
  //通过注解声明task的输出
  @OutputFile File destFile
  //在构造器中设置task的group和description
  ReleaseVersionTask(){
    group = 'versioning'
    description = 'Makes project a release version.'
  }
  //使用注解声明将被执行的方法
  @TaskAction
  void start(){
    project.version.release = true
    ant.propertyfile(file: destFile){
      entry(key: 'release',type: 'string',operation:'=',value:'true')
    }
  }
}
```
#### 通过注解表示输入和输出
上面的示例中，没有使用DefaultTask的属性来声明它的输入和输出。而是使用org.gradle.api.tasks包下的注解。
task的输入和输出注解为你的实现添加了语义糖。它们不仅与TaskInputs和TaskOutputs方法有相同的效果，而且它
们还能够充当自动文档。
> **task输入验证**

> @Input注解会在配置期间验证属性值。如果值为null，Gradle会抛出`TaskValidationException`异常。为了
> 允许输入为null值，我们需要给它添加@Optional注解。

#### 使用自定义task
通过创建一个动作方法和暴露它的配置属性，你实现了一个自定义的task类。那要如何使用它呢？在构建脚本中，你需要
创建一个ReleaseVersionTask类型的task，并且通过为它的属性赋值来设置输入和输出。
```gradle
//定义一个增强的ReleaseVersionTask类型的task
task makeReleaseVersion(type: ReleaseVersionTask){
  //设置自定义task属性
  release = version.release
  destFile = versionFile
}
```
与简单的task实现相比，增强的task的一个巨大优势在于所暴露的属性可以被单独赋值。
#### 应用自定义task的可重用性
假设你想要在另一个项目中使用自定义task。在那个项目中，需求是不同的。其版本POGO通过暴露不同的属性来
表示版本管理方案，示例：
```gradle
class ProjectVersion{
  Integer min
  Integer maj
  Boolean prodReady
  @Override
  String toString(){
    "$maj.$min${prodReady?'':'-SNAPSHOT'}"
  }
}
```
此外，项目所有者决定命名版本文件为`project-version.properties`，而不是`version.Propertis`。如何
让增强的task满足这些需求呢？其实你只需要给所暴露的属性赋予不同的值就可以了。示例：
```gradle
task makeReleaseVersion(type: ReleaseVersionTask){
  //POGO版本表示使用prodReady属性来指示发布标识
  release = version.prodReady
  //指定不同的版本文件对象
  destFile = file('project-version.properties')
}
```

### Gradle的内置task类型
#### 使用task类型
Gradle的内置task类型都是DefaultTask的派生类。因此，它们可以被构建脚本中的增强的task使用。Gradle提供
了广泛的task类型。但这里只使用了两个：`Zip`和`Copy`。示例：
```gradle
task createDistribution(type: Zip,dependsOn: makeReleaseVersion){
  //隐式引用War task的输出
  from war.outputs.files
  //把所有源文件都放到ZIP文件的src目录下
  from(sourceSets*.allSource){
    into 'src'
  }
  from(rootDir){
    //为ZIP文件添加版本文件
    include versionFile.name
  }
}
task backupReleaseDistribution(type:Copy){
  //隐式引用createDistribution的输出
  from createDistribution.outputs.files
  into "$buildDir/backup"
}
task release(dependsOn: backupReleaseDistribution) << {
  logger.quiet 'Releasing the project...'
}
```
> Zip和Copy继承自AbstractCopyTask，而AbstractCopyTask继承自DefaultTask。AbstractCopyTask的常用
> 方法有：
> +`form(sourcePaths: Object...)`
> +`from(sourcePath: Object,c: Closure)`，
> +`into(destDir: Object)`
> +`into(destPath: Object, c: Closure)`
