Gradle中使用到的Groovy语法
=======================
Gradle构建脚本就是可执行的Groovy脚本，但是它们却不能通过Groovy运行时环境运行。当需要实现自定
义的逻辑时，可以使用Groovy的语言特性在Gradle构建脚本中直接构建所需要的功能。

### 可选类型定义
Groovy并不强制你显示声明变量类型，方法参数或者返回类型。你可以使用`def`关键字进行简单的标识，它
作为`java.lang.Object`的一个占位符。在运行时，Groovy通过所赋的值分析出其类型。

```groovy
def buildTool = 'Gradle'
assert buildTool.class == java.lang.String
def initProjectVersion(major,minor){
  new ProjectVersion(major,minor)
}
assert initProjectVersion(1,2).class == com.manning.gia.ProjectVersion
```
这个特性在某种情况下非常有用，但是也许你还是喜欢显式的强类型（与`java`一样）。如果一个方法没有返
回值，则应该声明`void`而不是`def`作为返回值。

### 可选括号
在Gradle中如果方法签名需要至少一个参数的话，则方法调用可以省略括号。这个特性通常被用来创建更自然
的DSL。
```groovy
initProjectVersion(1,2)
initProjectVersion 1,2
println('Groovy is awesome!')
println 'Groovy is awesome'
```

### 字符串
在Groovy中有三种方式定义字符串。带单引号的字符串通常创建出等效于`Java`的`String`类型。第二种方
式与在`Java`中创建字符串的方式相同，即用双引号包起来。多个字符串，使用三个双引号包起来，这在赋值
长文本或者格式化（比如，多行SQL语句）时非常有用。
```groovy
def myString1 = 'This is a single-quoted String'
def myString2 = "This is a double-quoted String"
def myString3 =
"""
  This
  is a
  multiline
  String
"""
```

### Groovy字符串（GString）
Groovy中带双引号的字符串更强大。它们可以插值到变量或表达式中，通过`$`和大括号`{}`来表示。
```groovy
def language = 'groovy'
def sentence = "$language is awesome!"
assert sentence == 'groovy is awesome!'
def improvedSentence = "${language.capitalize()} is awesome!"
```

### 集合API
#### List
在方括号中放入一串以逗号`,`分隔的值，就可以初始化新的List。可以通过左移操作符`<<`向List中添加一
个新元素。
```groovy
def buildTools = ['Ant','Maven']
assert buildTools.getClass() == java.util.ArrayList
assert buildTools.size() == 2
assert buildTools[1] == 'Maven'

buildTools << 'Gradle'
assert buildTools.size() == 3
assert buildTools[2] == 'Gradle'

buildTools.each{ buildTool ->
  println buildTool
}
```
#### Map
在方括号`[]`中放入一串用逗号`,`分隔的键值对就可以创建一个新的`Map`。
```groovy
def inceptionYears = ['Ant':2000,'Maven':2004]
assert inceptionYears.getClass() == java.lang.LinkedHashMap
assert inceptionYears.size() == 2
assert inceptionYears.Ant == 2000
assert inceptionYears['Ant'] == 2000

inceptionYears['Gradle'] = 2009
assert inceptionYears.size() == 3
assert inceptionYears['Gradle'] == 2009

inceptionYears.each{ buildTool,year ->
  println "$buildTool was first released in $year"
}
```

### 命名参数
Groovy提供了一种更加方便的方式来设置属性值，叫作命名参数。这种机制首先调用类的默认构造器，然后为每
个参数调用对应的`setter`方法。
```groovy
class ProjectVersion{
  Integer major
  Integer minor
}
ProjectVersion projectVersion = new ProjectVersion(major:1,minor:10)
assert projectVersion.minor == 10
projectVersion.minor = 30
assert projectVersion.minor = 30
```

### 闭包
闭包是一个类型为`groovy.lang.Closure`的代码块，与其他编程语言的`lambdas`特性类似。闭包可以被
赋值给变量，作为参数传递给方法，并且像普通方法一样来调用。
#### 隐匿的闭包参数
每个没有显示定义任何参数的闭包都可以访问一个隐式的参数`it`。`it`代表调用这个闭包的时候第一个传递进
来的参数。如果没有提供参数，那么参数的值就是`null`。
```groovy
def incrementMajorProjectVersion = {  //隐式的闭包参数it
  it.major++
}
ProjectVersion projectVersion = new ProjectVersion(major:1,minor:10)
incrementMajorProjectVersion(projectVersion)
assert projectVersion.major == 2
```
#### 显式的闭包参数
除了使用隐式的闭包参数，你可以自定义更具有描述性的参数名。
```groovy
def incrementMajorProjectVersion = { ProjectVersion version ->  //显式闭包参数
  version.major++
}
ProjectVersion projectVersion = new ProjectVersion(major:1,minor:10)
incrementMajorProjectVersion(projectVersion)
assert projectVersion.major == 2
```
记住，在groovy中类型是可选的。你可以使用标识符`version`而不指定类型。groovy没有限制闭包可以指
定的参数数量。
```groovy
def setFullProjectVersion = { projectVersion,major,minor ->  //声明三个无类型闭包参数
  projectVersion.major = major
  projectVersion.minor = minor
}
ProjectVersion projectVersion = new ProjectVersion(major:1,minor:10)
setFullProjectVersion(projectVersion,2,1)
assert projectVersion.major == 2
assert projectVersion.minor == 1
```
#### 闭包返回值
闭包总是会返回一个值。返回值是闭包的最后一条语句的值（如果没有显式的`return`语句），或者是可执行
的`return`语句的值。如果闭包的最后一条语句没有值，就返回`null`。
```groovy
ProjectVersion projectVersion = new ProjectVersion(major:1,minor:10)
def minorVersion = {projectVersion.minor} //闭包返回最后一条语句的值
assert minorVersion() == 10
```
#### 闭包作为方法参数
```groovy
Integer incrementVersion(Closure closure,Integer count){  //闭包作为第一个参数
  closure() + count
}
ProjectVersion projectVersion = new ProjectVersion(major:1,minor:10)
assert incrementVersion({projectVersion.minor},2) == 12
```
#### 闭包委托
闭包代码在委托的闭包上执行。默认的，这个委托就是闭包的所有者。比如，如果你在groovy脚本中定义了一个
闭包，那么所有者就是一个`groovy.lang.Script`实例。闭包的隐式变量`delegate`允许你重新定义默认
的所有者。示例：
```groovy
class ProjectVersion{
  Integer major
  Integer minor
  void increment(Closure closure){  //ProjectVersion的内部方法用闭包作为参数
    closure.resolveStrategy = Closure.DELEGATE_ONLY   //只处理委托引用
    closure.delegate = This   //设置闭包的委托
    closure()   //调用闭包
  }
}
ProjectVersion projectVersion = new ProjectVersion(major:1,minor:10)
projectVersion.increment{major += 1}  //调用ProjectVersion实例的方法，其内部执行了一个闭包
assert projectVersion.major == 2
projectVersion.increment{minor += 5}  //调用ProjectVersion实例的方法，其内部执行了一个闭包
assert projectVersion.minor == 15
```

### 在Gradle构建脚本中使用Groovy
Gradle构建脚本就是合法的Groovy脚本。在构建脚本中，你可以使用Groovy语言的所有特性，这意味着代码与
Groovy语法严格吻合。示例：
```gradle
//调用Project的apply方法，关于参数，提供一个只有一个键值对的Map，方法调用省略括号
apply plugin: 'java'
//通过调用Project的setter方法为项目设置版本属性
version = '0.1'
//调用Project的repositories方法，并传递一个闭包参数
repositories{
  //调用闭包委托的mavenCentral()方法
  mavenCentral()
}
//调用Project的dependencies方法，并传递一个闭包参数
dependencies{
  //调用闭包委托的对象的compile方法，用一个String作为参数，方法调用省略括号
  compile 'commons-codec:commons-codec:1.6'
}
```
