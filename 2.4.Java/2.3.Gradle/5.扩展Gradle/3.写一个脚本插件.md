写一个脚本插件
=====================
一个脚本插件和一个普通的`build.gradle`文件没有什么区别。你可以使用同样的Gradle构建语言。新建一个名字
为`cloudbees.gradle`的脚本文件，里面会包含Cloudbees的task。因为构建脚本的名字不遵从默认的命名约定，
所以你需要使用`-b`命令行选项来显式指定使用该脚本。执行命令：
```powershell
$ gradle -b cloudbees.gradle tasks
```
应该会显示默认的帮助任务。

### 添加Cloudbees的API类库
要在构建脚本中直接使用一个外部类库，你需要在`classpath`下声明它。为此，Gradle的API类`org.gradle.api.Project`
暴露了一个名为`buildscript`的方法。该方法期待一个参数，一个闭包，里面定义了你想要解析的依赖，该依赖定义
在`classpath`配置分组中。Cloudbees的API客户端类库可以从Maven Central获得。你可以通过调用mavenCentral()
方法指定仓库。示例：
```gradle
buildscript{
  repositories{
    //使用Maven Central来解析已声明的依赖
    mavenCentral()
  }
  dependencies{
    //将Cloudbees客户端API类库添加到构建脚本的classpath中
    classpath 'com.cloudbees:cloudbees-api-client:1.4.0'
  }
}
```
当脚本第一次被执行时，Cloudbees类库会被下载并保存到本地依赖缓存中。

### 准备客户端属性配置
Cloudbees API的核心类是客户端的实现：`com.cloudbees.api.BeesClient`。在实例化时，该类希望你可以提供
帐户信息，API的URL，格式以及版本。例如：
```groovy
BeesClient client = new BeesClient('https://api.cloudbees.com/api','24HE9X5DFF743671',
  '24QSXAHS1LAAVWDFAZS3TUFE6FZHK1DBYA=','xml','1.0')
```
#### 准备客户端属性配置
API参数，你需要将它们定义为外部属性，就像下面这样：
```gradle
ext{
  apiUrl = 'https://api.cloudbees.com/api'
  apiFormat = 'xml'
  apiVersion = '1.0'
}
```
你应该不会想要将API密钥和私钥分享出去或提交到版本控制中。你应该创建`gradle.properties`文件来保存这两个值。
读取这两个值，然后把它们存储在属性`apiKey`和`secret`中，示例：
```gradle
if(project.hasProperty('cloudbeesApiKey')){
  ext.apiKey = project.property('cloudbeesApiKey')
}
if(project.hasProperty('cloudbeesApiSecret')){
  ext.secret = project.property('cloudbeesApiSecret')
}
```
#### 获取应用信息
当设置Cloudbees账号时，你配置了一个名为todo的应用。Cloudbees的API能够在不登录到控制面板的情况下远程获取
关于该应用的信息，示例：
```gradle
import com.cloudbees.api.ApplicationInfo
import com.cloudbees.api.BeesClient
task cloudBeesAppInfo(description:'Returns the basic information about an application.',
  group:'Cloudbees'){
  //给task声明输入属性，如果不提供任何属性，则task执行失败
  inputs.property('apiKey',apiKey)
  inputs.property('secret',secret)
  inputs.property('appId',appId)
  doLast{
    //Cloudbees的SDK客衣端实现暴露对所有服务的访问
    BeesClient client = new BeesClient(apiUrl,apiKey,secret,apiFormat,apiVersion)
    ApplicationInfo info
    try{
      //通过指定appId属性获取关于应用的信息
      info = client.ApplicationInfo(appId)
    }catch(Exception e){
      //任何执行失败都当作异常（比如，客户端的授权异常），并作为特殊的Gradle异常捕获和重抛
      throw new GradleException(e.message)
    }
    //使用Gradle日志来打印响应信息到控制台
    logger.quiet "Application id ： $info.id"
    logger.quiet "         title ： $info.title"
    logger.quiet "       created ： $info.created"
    logger.quiet "          urls ： $info.urls"
    logger.quiet "        status ： $info.status"
  }
}
```
在执行该任务之前，你需要创建另一个Gradle脚本文件：`build.gradle`。示例展示了如何重用外部脚本文件
`cloudbees.gradle`：
```gradle
apply from: 'cloudbees.gradle'
```
请注意apply方法调用时传入的from属性，它的值可以是任何类型的URL，比如HTTP地址
http://my.scripts.com/shared/cloudbees.gradle 。以基于HTTP(S)的方式暴露脚本插件是一个组织的部门
间共享的较好办法。现在运行`cloudBeesAppInfo`任务：
```powershell
$ gradle -PappId=gradle-in-action/todo cloudBeesAppInfo
```
#### 部署一个WAR文件
编写一个task通过Cloudbees的客户端API部署WAR文件，示例：
```gradle
import com.cloudbees.api.ApplicationDeployArchiveResponse
import com.cloudbees.api.BeesClient
task cloudBeesDeployWar(
  description:'Deploys a new version of an application using a WAR archive file.',
  group:'Cloudbees'){
  //除了前面task鉴别出来的task输入属性，还要确保提供了WAR文件
  inputs.property('apiKey',apiKey)
  inputs.property('secret',secret)
  inputs.property('appId',appId)
  inputs.file file(warFile)
  ext.message = project.hasProperty('message')?project.message:null
  inputs.property('message',message)
  doLast{
    logger.quiet "Deploying WAR '$warFile' to application ID '$appId' with message '$message'"
    BeesClient client = new BeesClient(apiUrl,apiKey,secret,apiFormat,apiVersion)
    ApplicationDeployArchiveResponse response
    try{
      response = client.applicationDeployWar(appId,null,message,file(warFile),null,null)
    }catch(Exception e){
      throw new GradleException("Error: $e.message")
    }
    logger.quiet "Application uploaded successfully to: '$response.url'"
  }
}
```
执行`cloudBeesDeployWar`任务将todo应用部署到云端：
```powershell
$ gradle -PappId=gradle-in-action/todo -PwarFile=todo.war -Pmessage=v0.1 cloudBeesDeployWar
```
