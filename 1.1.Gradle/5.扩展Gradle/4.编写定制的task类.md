编写定制的task类
========================
前一节，你创建了一个可以和PaaS服务商交互的可共享脚本，但它也是有优点和缺点的：

**优点:**
+ task都是可利用的，并且可以导入到其他项目中。
+ task是可配置的。消费脚本仅仅需要知道必要的输入。
+ 最新状态检查功能可以通过task的输入和输出属性使用。

**缺点：**
+ task的逻辑是通过action的闭包定义的，因此不能够放入类和包中。
+ 添加的task越多，构建脚本就越长，越难维护。
+ 不能进行单元测试和集成测试

### 定制task的实现选项
Gradle提供了一个可以继承的默认实现：`org.gradle.api.DefaultTask`。实际上，许多Gradle标准插件的task
都继承自DefaultTask。

Gradle有多种定义定制化task类的方式。最简单的方式就是把它和构建代码一起放在构建脚本里。当触发脚本中的一个
task时，定制的task会自动编译并放到`classpath`中。

另一种方式就是将定制的task放到项目根目录下的`buildSrc`目录中。要确保遵从语言插件定义的源代码目录约定。
例如，如果你用Java语言编写定制的task，就要将它放到`buildSrc/src/main/java`目录下。Gradle会将该目录
当作默认的源代码目录，当运行构建时，自动尝试编译所有的源代码文件。记住，Gradle的增量构建特性在这里也是支持的。
位于 **`buildSrc`目录下的定制的task类被所有的项目构建脚本共享** ，并且在`classpath`中自动可用。
为了使定制和task可以在多个项目中共享，你可以将它们打成JAR文件，然后在构建脚本的`classpath`中定义。

编写定制task类的三种方式：
+ 在构建脚本中定制任务类
+ 在项目的`buildSrc`目录下定制任务类
+ 把编写的task类打成JAR包，在构建脚本build.gradle中使用

### 在buildSrc下定义定制任务
现在，来试试方式2。你需要在buildSrc目录下创建定制task源文件。现在项目结构变成这样：
```
|----build.gradle                   //使用方构建脚本
|----buildSrc
|    |----build.gradle              //用来定义buildSrc的脚本
|    |----src
|         |----main
|              |----groovy
```
我们现在来重写从CloudBees获取应用信息的简单task，代码如下：
```groovy
package com.manning.gia.plugins.cloudbees.tasks.app

import com.cloudbees.api.ApplicationInfo
import com.cloudbees.api.BeesClient
import org.gradle.api.*
import org.gradle.api.tasks.*

class CloudBeesAppInfo extends DefaultTask{
  //可配置的task属性
  @Input String apiUrl
  @Input String apiKey
  @Input String secret
  @Input String apiFormat
  @Input String apiVersion
  @Input String appId
  CloudBeesAppInfo(){
    //指定task的group和描述信息
    description = 'Returns the basic information about an application.'
    group = 'CloudBees'
  }
  //由注解指定的执行方法
  @TaskAction
  void start(){
    BeesClient client = new BeesClient(apiUrl,apiKey,secret,apiFormat,apiVersion)
    ApplicationInfo info
    try{
      info = client.ApplicationInfo(appId)
    }catch(Exception e){
      throw new GradleException(e.message)
    }
    logger.quiet "Application id: $info.id"
    logger.quiet "         title: $info.title"
    logger.quiet "       created: $info.created"
    logger.quiet "          urls: $info.urls"
    logger.quiet "        status: $info.status"
  }
}
```
task的行为封装在task的action中。要指定哪个action执行，需要在方法上加上注解@TaskAction。task执行
方法的名字可以任意取，只要不覆盖父类的`void execute()`方法就行。

在定制task的实现中，你需要引入CloudBees类库中的类。为了确保类文件可以被正确编译，你需要给`buildSrc`
工程创建一个构建脚本`build.gradle`文件，并声明CloudBees类库：
```gradle
repositories{
  mavenCentral()
}
dependencies{
  compile 'com.cloudbees:cloudbees-api-client:1.4.0'
}
```
#### 使用定制和task
定制的task类不能由自己直接执行。要使用和配置由定制task定义的行为，你需要创建一个增强型的task。该task定
义了它要使用的task类型：
```gradle
//导入的定制任务类
import com.manning.gia.plugins.cloudbees.tasks.app.CloudBeesAppInfo
//定制任务的使用
task CloudBeesAppInfo(type: CloudBeesAppInfo){
  apiUrl = project.apiUrl
  apiKey = project.apiKey
  secret = project.secret
  apiFormat = project.apiFormat
  apiVersion = project.apiVersion
  appId = project.hasProperty('appId')?project.appId:null
}
```
#### 通过重构提高可重用性
当比较两个CloudBees的定制任务时，你会发现它们的实现在结构上非常相似的。我们可以确定它们有下面这些共同点：
+ 两个类都创建了CloudBees的API客户端实例BeesClient。
+ 它们都需要提供CloudBees的API选项和认证信息。
+ 当CloudBees的API交互时，你需要捕获一个异常并适当处理该异常。
+ 所有的CloudBees定制task都需要分配给group CloudBees。

我们可以通过创建一个父类，你可以大大地简化当前的CloudBees定制task的代码。
```groovy
package com.manning.gia.plugins.cloudbees.tasks

import com.cloudbees.api.BeesClient
import com.gradle.api.*
import com.gradle.api.tasks.*

abstract class CloudBeesTask extends DefaultTask{
  //为API认证信息暴露属性
  @Input String apiFormat = 'xml'
  @Input String apiVersion = '1.0'
  @Input String apiUrl = 'https://api.cloudbees.com/api'
  @Input String apiKey
  @Input String secret
  CloudBeesTask(String description){
    this.description = description
    //指定默认的task的group名字
    group = 'CloudBees'
  }
  @TaskAction
  void start(){
    withExceptionHandling{
      BeesClient client = new BeesClient(apiUrl,apiKey,secret,apiFormat,apiVersion)
      executeAction(client)
    }
  }
  //捕获异常并处理
  private void withExceptionHandling(Closure c){
    try{
      c()
    }catch(Exception e){
      throw new GradleException(e.message)
    }
  }
  //由子类实现的抽象方法
  abstract void executeAction(BeesClient client)
}
```
获取应用信息的task实现，继承CloudBeesTask类。示例：
```groovy
package com.manning.gia.plugins.cloudbees.tasks.app

import com.cloudbees.api.ApplicationInfo
import com.cloudbees.api.BeesClient
import com.gradle.api.tasks.Input
import com.manning.gia.plugins.cloudbees.tasks.CloudBeesTask

//扩展CloudBees的父task
class CloudBeesAppInfo extends CloudBeesTask{
  //针对这个task的功能暴露指定的属性
  @Input String appId
  CloudBeesAppInfo(){
    //提供任务描述
    super('Returns the basic information about an application.')
  }
  //实现任务的action，提供已经创建的CloudBees的API客户端实例
  ＠Override
  void executeAction(BeesClient client){
    ApplicationInfo info = client.ApplicationInfo(appId)
    logger.guiet "Application title: $info.title"
    logger.guiet "          created: $info.created"
    logger.guiet "             urls: $info.urls"
    logger.guiet "           status: $info.status"
  }
}
```
