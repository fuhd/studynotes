配置子项目
=========================
本节中，你将了解到在多项目构建中如何定义特定的和公共的行为来避免重复配置。

### 理解Project API表示
前面我们了解过Project API的属性和方法，你可能会在日常业务中使用到它们。为了实现多项目构建，你还需要
了解一些新的方法。
```groovy
//project API的部分属性和方法

//特定的项目配置
project(path:String)
project(path:String,config:Closure)
//公共的项目配置
allprojects(action:Action<? super Project>)
allprojects(action:config:Closure)
subprojects(action:Action<? super Project>)
subprojects(action:config:Closure)
//项目解析顺序
evaluationDependsOn(path:String)
evaluationDependsOnChildren()
```
为了声明特定的项目构建代码，使用了`project`方法。至少必须提供项目路径（例如，`:model`）。

很多时候，你想要为所有的项目或只有子项目定义一些公共的行为。为实现这些用例，Project API提供了两个专门的
方法：`allprojects`和`subprojects`。假设你想要把Java插件应用到所有子项目中，你可以通过在subprojects
闭包参数中定义代码来实现。

在多项目构建中项目的默认执行顺序是基于它们的字母名称的。为了显式地控制在构建生命周期的配置阶段的执行顺序，
你可以使用方法：`evaluationDependsOn`和`evaluationDependsOnChildren`。对于需要确保为一个项目设置
的属性可以被其他项目使用的情况尤其如此。

### 定义特定的行为
可以通过project方法来定义特定的项目行为。示例，为了给三个子项目————model，repository，web搭建构建基础
环境，你需要为它们分别创建一个项目配置块，见下面的`build.gradle`文件：
```gradle
//声明额外属性projectIds，通过map的形式存储group和version键值对；这些属性可以在子项目中使用
ext.projectIds = ['group':'com.manning.gia','version':'0.1']
group = projectIds.group
version = projectIds.version
//通过project方法配置每一个子项目，实际的配置在闭包中
project(':model'){
  group = projectIds.group
  version = projectIds.version
  apply plugin: 'java'
}
project(':repository'){
  group = projectIds.group
  version = projectIds.version
  apply plugin: 'java'
}
project(':web'){
  group = projectIds.group
  version = projectIds.version
  apply plugin: 'java'
  apply plugin: 'war'
  apply plugin: 'jetty'
  repositories{
    mavenCentral()
  }
  dependencies{
    providedCompile 'javax.servlet:servlet-api:2.5'
    runtime 'javax.servlet:jstl:1.1.2'
  }
}
```
你会发现解决方案并不完美。即使定义了一个额外属性，将group和version属性值指定给每一个子项目，也还是有
一些重复代码。
> #### 属性继承
> 在一个项目中定义的属性会自动被其子项目继承，这个概念也适用于其他构建工具如Maven。额外属性projectIds
> 是在根项目中声明的，它对于model，repository和web子项目都是可用的。

在多项目构建中，你可以从根目录为某个子项目执行task。你只需要确定连接项目路径和task名称即可。记住，路径
是用冒号（`:`）表示的。示例：
```powershell
$ gradle :model:build
```

### 声明项目依赖
声明依赖另一个项目和声明依赖外部类库看起来很相似。在这两种情况下，依赖必须在dependencies配置块的闭包中
声明。必须为项目依赖指定一个特定的配置，下面这个例子中，配置compile是Java插件提供了。示例：
```gradle
//model子项目不需要声明任何外部依赖或项目依赖
project(':model'){
  //.....
}
project(':repository'){
  //...
  dependencies{
    //使用:model方式声明编译时依赖项目
    compile project(':model')
  }
}
project(':web'){
  //...
  dependencies{
    //使用:repository方式声明编译时依赖项目
    compile project(':repository')
    providedCompile 'javax.servlet:servlet-api:2.5'
    runtime 'javax.servlet:jstl:1.1.2'
  }
}
```
子项目repository依赖于子项目model，子项目web依赖于子项目repository。建模项目依赖关系就是这样的。这样
做有三个重要的含义：
+ 一个项目依赖关系的实际依赖是它自己创建的库。对于子项目model的情况，它就是JAR文件。这就是为什么一个项目
依赖关系也被称为库依赖的原因。
+ 对另一个项目的依赖也要将其传递性依赖添加到classpath中。这意味着外部依赖和其他项目依赖也被添加进去。
+ 在构建生命周期的初始化阶段，Gradle确定了项目的执行顺序。对另一个子项目的依赖意味着这个子项目必须被
事先构建，毕竟你要依赖它的库。

#### 从根项目执行task
经过初始化阶段后，Gradle有了一个项目依赖的内部模型。它知道子项目repository依赖于model，子项目web依赖于
repository。你不需要从一个特定的子项目执行task————你可以为构建的所有项目执行task。假设你想要从根项目执行
build task：
```powershell
$ gradle build
```
事实上，Gradle知道子项目的执行顺序。从根项目执行task节省了项目的构建时间。Gradle从所有子项目执行task，包
括支持增量式构建。这种做法很方便，而且确保在classpath中总是有最新的类文件。

### 多项目部分构建
Gradle提供了部分构建特性。部分构建是通过命令行选项`-a`或`--no-rebuild`启用的。假设在子项目repository中
只改变了代码，但不想重新构建子项目model。通过使用部分构建，可以省去检查子项目model的成本并且降低构建执行时
间。如果你正工作在一个拥有上百个子项目依赖的企业级项目中。你会感激在执行构建时节省的每一秒。示例：
```powershell
$ gradle :repository:build -a
```
作为日常开发实践的一部分，你想要从仓库中拉取源代码的最新版本，并集成团队成员所做的更改。为了确保代码不会出
现异常，你需要重新构建和测试当前项目所依赖的项目。通常`build` task **只编译依赖项目的代码，组装成JAR文件，
并且使其作为其他项目的依赖** 。为了 **运行测试** ，可以执行`buildNeeded` task。示例如下：
```powershell
$ gradle :repository:buildNeeded
```
项目的任何改变都可能对 **依赖于它的其他项目** 产生副作用，在`buildDependents` task的帮助下，通过构建和
测试依赖的项目来验证代码变化所产生的影响。示例：
```powershell
$ gradle :repository:buildDependents
```

### 声明跨项目的task依赖
#### 默认的task执行顺序
假设你在根项目以及所有子项目中定义了一个名为hello的task，示例：
```gradle
//为根项目和所有子项目声明一个具有相同名字的task
task hello << {
  println 'Hello from root project'
}
project(':model'){
  task hello << {
    println 'Hello from model project'
  }
}
project(':repository'){
  task hello << {
    println 'Hello from repository project'
  }
}
```
如果从根项目运行这个hello task，你将看到如下输出：
```powershell
$ gradle hello
```
```
:hello
Hello from root project
:model:hello
Hello from model project
:repository:hello
Hello from repository project
```
这些task都没有声明依赖另一个task。那么Gradle如何知道按什么顺序来执行task呢？很简单，在多项目构建中
位于根级别的task总是首先执行。对于子项目来说，执行顺序完全取决于项目名称的字母顺序。model在repository
之前。记住，在settings.gradle文件中子项目的声明顺序完全不影响其执行顺序。
#### 控制task执行顺序
你可以通过声明一个跨项目的task依赖来确定task执行顺序。为此，你需要引用到不同项目中task的路径。下面的
示例演示了如何确保子项目repository中的hello task在子项目model之前被执行：
```gradle
task hello << {
  println 'Hello from root project'
}
project(':model'){
  //声明一个task依赖于子项目仓库中的task
  task hello(dependsOn:':repository:hello') << {
    println 'Hello from model project'
  }
}
project(':repository'){
  task hello << {
    println 'Hello from repository project'
  }
}
```

### 定义公共行为
在定义特定的行为一节中的示例，我们说过了，不完美，因为有重复声明。这里我们将通过使用`allprojects`和
`subprojects`方法来改进现有代码。对于项目这意味着什么呢？你可以使用`allprojects`方法来设置根项目和
子项目的group和version属性。由于根项目没有定义Java代码，所以不需要使用Java插件，只有子项目需要Java
插件。你可以使用`subprojects`方法将Java插件应用于所有的子项目。示例：
```gradle
//为根项目和所有子项目设置group和version属性
allprojects{
  group = 'com.mannig.gia'
  version = '0.1'
}
//设置Java插件只应用于子项目
subprojects{
  apply plugin:'java'
}
project(':repository'){
  dependencies{
    //好像外面都没有直接定义:model的project
    compile project(':model')
  }
}
project(':web'){
  apply plugin: 'war'
  apply plugin: 'jetty'
  repositories{
    mavenCentral()
  }
  dependencies{
    compile project(':repository')
    providedCompile 'javax.servlet:servlet-api:2.5'
    runtime 'javax.servlet:jstl:1.1.2'
  }
}
```
